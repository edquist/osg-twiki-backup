# This awk script with "clean up" the OSG TWiki Glossary html file 
if [ $# -ne 2 ]
then
echo "Usage: cleanglossary glossaryofterms.html CleanGlossary.html"
exit
fi

awk '
# Fix title
/<title>/ {
   print "<title> OSG Glossary </title>";
   next;
}
/<body/ {
   print "<body>";
   print "<h1> OSG Glossary of Terms </h1>";
   next;
}

# Remove <base> so internal references will be internal
/^<base/ { next; }

# Remove EDITTHIS buttons
/EDITTHIS/ { next; }

# Remove last line (error in html page)
/<\/html>/ {
   print;
   exit;
}

# Remove Scripts, Styles, and Imports, meta, divs, classes
/<script /,/<\/script>/ { next; }
/<style /,/<\/style>/ { next; }
/\@import/ { next; }
/<\/style>/ { next; }
/<link / { next; }
/<meta name/ { next; }
/<div / { next; }
/<\/div>/ { next; }

# Remove forms (that implement the letter indexes)
/<form/,/<\/form>/ { next; }
/<span style/ { next; }

# Remove the Comment Boxes
/commentPlugin/ { next; }

# Remove Bottom of Page links
/GlossaryToPDF/ { next; }
/CONTENT MANAGEMENT PROJECT/,/^<\/font>/ { next; }

# Make internal page links internal (not refering to TWiki)
/href=.\/bin\/view\/Documentation\/GlossaryOfTerms#/ {
    n = gsub("\/bin\/view\/Documentation\/GlossaryOfTerms#", "#", $0);
}

# Make references to other TWiki pages a true external reference
/href="\/bin/ {
    n = gsub("\/bin", "https://twiki.grid.iu.edu/bin", $0);
}

# Modify Title
/Glossary of Terms/ { 
    n = gsub("Glossary of", "OSG Glossary of", $0);
}

# Style the h2 (Letter) headers
/<\/head>/ {
 print "<style type=\"text/css\">";
 print "h2 {";
 print " background-color:orange;";
 print " border-bottom-style:solid;";
 print "}";
 print "</style>";
}

# This merely copies the content lines
{ print; }
' $1 >$2
